// =========================================
// =============== æ°´å¹³æ ‡ç­¾æ  ===============
// =========================================

import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Layouts 1.15

import "../Widgets"

RowLayout  {
    id: hTabBarLayout
    anchors.fill: parent
    spacing: 0

    // æ ‡ç­¾æ æ§åˆ¶ï¼ˆå·¦ï¼Œç½®é¡¶æŒ‰é’®ï¼‰
    Item  {
        width: size_.hTabBarHeight
        Layout.fillHeight: true
        // å›¾é’‰æŒ‰é’®
        Button {
            checkable: true
            checked: mainWindowRoot.isMainWindowTop
            onCheckedChanged: { // åŒå‘ç»‘å®šé”å®šæ ‡è®°
                mainWindowRoot.isMainWindowTop = checked
                qmlapp.globalConfigs.setValue("window.isMainWindowTop", checked, true)
            }
            anchors.fill: parent
            anchors.margins: 4

            contentItem: Icon_ {
                icon: "pin"
                anchors.fill: parent
                color: parent.checked ? theme.bgColor : theme.textColor
            }

            background: Rectangle {
                anchors.fill: parent
                radius: size_.btnRadius
                color: parent.checked ? theme.coverColor4 : (
                    parent.hovered ? theme.coverColor2 : "#00000000"
                )
            }

            ToolTip_ {
                visible: parent.hovered
                text: qsTr("çª—å£ç½®é¡¶")
            }
        }
    }

    // æ ‡ç­¾æ æœ¬ä½“ï¼ˆä¸­ï¼‰
    Rectangle  {
        id: hTabBarMain
        Layout.fillWidth: true
        Layout.fillHeight: true
        color: "#00000000"

        property int tabWidth: 200 // æ ‡ç­¾å½“å‰å®½åº¦

        // æ–¹æ³•ï¼šé‡è®¾æ ‡ç­¾æŒ‰é’®å®½åº¦
        function resetTabBtnWidth() {
            let w = hTabBarMain.width
            if(!qmlapp.tab.barIsLock) w -= tabBarControl.width // æ— é”å®šæ—¶ï¼Œå‡å»+æŒ‰é’®å®½åº¦
            w = w / barManager.model.count
            tabWidth = Math.min(w, size_.line * 8)
        }
        onWidthChanged: resetTabBtnWidth()  // ç›‘å¬æ ‡ç­¾æ æ€»å®½åº¦å˜åŒ–
        // ç›‘å¬æ”¹å˜é”å®šï¼Œé‡è®¾å®½åº¦
        property bool isLock: qmlapp.tab.barIsLock
        onIsLockChanged: {
            hTabBarMain.resetTabBtnWidth()
        }

        MouseArea { // ç‚¹å‡»æ ‡ç­¾æ ç©ºä½™ä½ç½®ï¼Œéƒ½æ˜¯æ·»åŠ æ–°æ ‡ç­¾
            anchors.fill: parent
            onClicked: {
                if(!qmlapp.tab.barIsLock)
                    qmlapp.tab.addNavi() // æ·»åŠ å¯¼èˆªé¡µ
            }
        }

        // Rectangle { // æ ‡ç­¾æŒ‰é’®ä¸‹æ–¹çš„é˜´å½±
        //     anchors.bottom: parent.bottom
        //     width: parent.width
        //     height: size_.hTabBarHeight * 0.5
        //     gradient: Gradient {
        //         GradientStop { position: 0.0; color: "#00000000" }
        //         GradientStop { position: 1.0; color: theme.coverColor2 }
        //     }
        // }

        Rectangle { // æ‹–æ‹½æ—¶çš„ä½ç½®æŒ‡ç¤ºå™¨
            id: dragIndicator
            visible: false
            width: parent.tabWidth
            height: size_.hTabBarHeight
            gradient: Gradient { // æ°´å¹³æ¸å˜
                orientation: Gradient.Horizontal
                GradientStop { position: 1.0; color: "#00000000" }
                GradientStop { position: 0.0; color: theme.coverColor3 }
            }
        }

        // æ°´å¹³æ ‡ç­¾æ è¡Œå¸ƒå±€
        Row {
            id: hTabBarMainRow
            spacing: -1 // ç»™è´Ÿçš„é—´éš”ï¼Œæ˜¯ä¸ºäº†è®©é€‰ä¸­æ ‡ç­¾èƒ½è¦†ç›–å·¦å³ä¸¤è¾¹æ ‡ç­¾çš„ç«–çº¿

            // ===== æ ‡ç­¾æŒ‰é’®ç»„ =====
            BarManager {
                id: barManager
                // æ ‡ç­¾å…ƒç´ æ¨¡æ¿
                delegate: TabButton_ {
                    title: title_ // æ ‡é¢˜
                    checked: checked_ // åˆå§‹æ—¶æ˜¯å¦é€‰ä¸­
                    index: index_ // åˆå§‹ä½ç½®
                    width: hTabBarMain.tabWidth
                }

                // äº‹ä»¶ï¼šåˆ›å»ºæ–°æ ‡ç­¾æ—¶ï¼ˆä¸çˆ¶ç±»çš„æ§½åŒæ—¶ç”Ÿæ•ˆï¼‰
                onItemAdded: { 
                    // é“¾æ¥è¡¨ç°ç›¸å…³çš„æ§½å‡½æ•°
                    item.dragStart.connect(dragStart)
                    item.dragFinish.connect(dragFinish)
                    item.dragMoving.connect(dragMoving)
                }

                // äº‹ä»¶ï¼šæŒ‰é’®æ•°é‡å˜åŒ–
                onCountChanged: hTabBarMain.resetTabBtnWidth()

                // ========================= ã€æ‹–æ‹½ç›¸å…³ã€‘ =========================

                property var intervalList: [] // è®°å½•æŒ‰é’®ä½ç½®åŒºé—´çš„åˆ—è¡¨
                property var originalPosList: [] // è®°å½•æŒ‰é’®åˆå§‹ä½ç½®çš„åˆ—è¡¨
                property int originalX // è®°å½•æœ¬è½®æ‹–æ‹½å‰ï¼Œè¢«æ‹–æ‹½æŒ‰é’®åŸæœ¬çš„ä½ç½®
                function dragStart(index){ // æ–¹æ³•ï¼šå¼€å§‹æ‹–æ‹½
                    // é‡æ–°è®°å½•å½“å‰æ‰€æœ‰æŒ‰é’®çš„ä½ç½®
                    originalX = itemAt(index).x
                    intervalList = [-Infinity] // ä¸‹é™ï¼šè´Ÿæ— ç©·
                    originalPosList = [itemAt(0).x]
                    for(let i=1, c=model.count; i < c; i++){ // æŒ‰é’®ä½ç½®åŒºé—´
                        const it = itemAt(i)
                        intervalList.push(it.x)
                        originalPosList.push(it.x)
                    }
                    intervalList.push(Infinity) // ä¸Šé™ï¼šè´Ÿæ— ç©·
                    dragIndicator.visible = true

                }
                function btnDragIndex(index){ // å‡½æ•°ï¼šè¿”å›å½“å‰indexåº”è¯¥æ‰€å¤„çš„åºå·
                    const dragItem = itemAt(index)
                    const x = dragItem.x + Math.round(dragItem.width/2) // è¢«æ‹–åŠ¨æŒ‰é’®çš„ä¸­å¿ƒä½ç½®
                    let go = 0 // åº”è¯¥æ‹–æ”¾åˆ°çš„ä½ç½®
                    for(const c=intervalList.length-1; go < c; go++){
                        if(x >= intervalList[go] && x <= intervalList[go+1]){
                            break;
                        }
                    }
                    return go;
                }
                function dragMoving(index, x){ // æ–¹æ³•ï¼šæ‹–æ‹½ç§»åŠ¨
                    let go = btnDragIndex(index) // åº”è¯¥æ‹–æ”¾åˆ°çš„åºå·
                    dragIndicator.x = originalPosList[go]
                }
                function dragFinish(index){ // æ–¹æ³•ï¼šç»“æŸæ‹–æ‹½
                    dragIndicator.visible = false
                    let go = btnDragIndex(index) // åº”è¯¥æ‹–æ”¾åˆ°çš„åºå·
                    if(index !== go){ // éœ€è¦ç§»åŠ¨
                        // model.move(index, go, 1)
                        qmlapp.tab.moveTabPage(index, go)
                    } else { // æ— éœ€ç§»åŠ¨ï¼Œåˆ™å›åˆ°åŸä½
                        itemAt(index).x = originalX
                    }
                    resetIndex()
                }
            }
            
            // å…ƒç´ ï¼šæ§åˆ¶æŒ‰é’®
            Rectangle{
                id: tabBarControl
                color: "#00000000"
                width: size_.hTabBarHeight
                height: size_.hTabBarHeight
                visible: !qmlapp.tab.barIsLock

                // æ·»åŠ â€œ+â€æŒ‰é’®
                IconButton {
                    anchors.fill: parent
                    anchors.margins: 4
                    icon_: "add"
                    color: theme.textColor
                    onClicked: {
                        qmlapp.tab.addNavi() // æ·»åŠ å¯¼èˆªé¡µ
                    }
                }
            }

            // åŠ¨ç”»
            add: Transition { // æ·»åŠ å­é¡¹
                enabled: qmlapp.enabledEffect
                NumberAnimation {
                    properties: "opacity, scale" // é€æ˜åº¦å’Œå¤§å°ä»å°åˆ°å¤§
                    from: 0; to: 1.0
                    easing.type: Easing.OutBack // ç¼“åŠ¨ï¼šè¶…å‡ºåå¼¹
                    duration: 300
                }
            }
            move: Transition { // ç§»åŠ¨å­é¡¹
                enabled: qmlapp.enabledEffect
                NumberAnimation {
                    properties: "x,y"
                    easing.type: Easing.OutBack
                    duration: 300
                }
            }
        }
    }

    // æ ‡ç­¾æ æ§åˆ¶ï¼ˆå³ï¼Œé”å®šæŒ‰é’®ï¼‰
    Item{
        width: size_.hTabBarHeight
        Layout.fillHeight: true

        // é”å®šâ€œğŸ”’ï¸â€æŒ‰é’®
        Button {
            checkable: true
            checked: qmlapp.tab.barIsLock
            onCheckedChanged: { // åŒå‘ç»‘å®šé”å®šæ ‡è®°
                qmlapp.tab.barIsLock = checked
                qmlapp.globalConfigs.setValue("window.barIsLock", checked, true)
            }
            anchors.fill: parent
            anchors.margins: 4

            contentItem: Icon_ {
                icon: "lock"
                anchors.fill: parent
                color: parent.checked ? theme.bgColor : theme.textColor
            }

            background: Rectangle {
                anchors.fill: parent
                radius: size_.btnRadius
                color: parent.checked ? theme.coverColor4 : (
                    parent.hovered ? theme.coverColor2 : "#00000000"
                )
            }

            ToolTip_ {
                visible: parent.hovered
                text: qsTr("é”å®šæ ‡ç­¾æ ")
            }
        }
    }
}