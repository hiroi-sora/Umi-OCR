// ============================================
// =============== OCRå›¾ç‰‡æµè§ˆå™¨ ===============
// ============================================

import QtQuick 2.15
import QtQuick.Controls 2.15
import "../"

Item {
    // ========================= ã€æ¥å£ã€‘ =========================

    // è®¾ç½®å›¾ç‰‡æºï¼Œå±•ç¤ºä¸€å¼ å›¾ç‰‡
    function setSource(source) {
        hasTextBoxes = false
        // ç‰¹æ®Šå­—ç¬¦#æ›¿æ¢ä¸º%23
        if(source.startsWith("file:///") && source.includes("#")) {
            source = source.replace(new RegExp("#", "g"), "%23");
        }
        showImage.source = source // è®¾ç½®æº
        if(showImage.source == "") {
            imageScale = 1.0
            return
        }
        imageW = showImage.sourceSize.width // è®°å½•å›¾ç‰‡å®½é«˜
        imageH = showImage.sourceSize.height
        imageScaleFull()
    }

    // å±•ç¤ºå›¾ç‰‡åŠ OCRç»“æœ
    function setSourceResult(source, res) {
        setSource(source)
        // æ ¼å¼è½¬æ¢
        if(res.code == 100 && res.data.length > 0) {
            let tbs = []
            for(let i in res.data) {
                const d = res.data[i]
                const info = {
                    x: d.box[0][0],
                    y: d.box[0][1],
                    width: d.box[2][0] - d.box[0][0],
                    height: d.box[2][1] - d.box[0][1],
                    text: d.text
                }
                tbs.push(info)
            }
            textBoxes = tbs
            hasTextBoxes = true
        }
    }

    // ========================= ã€å¤„ç†ã€‘ =========================

    // æ ¹æ®ä¸­å¿ƒä½ç½®ï¼Œæ›´æ–°Imageçš„å›¾ç‰‡å®é™…ä½ç½®
    function updateImageXY() {
        flickable.contentY =  - (flickable.height - showImageContainer.height)/2
        flickable.contentX =  - (flickable.width - showImageContainer.width)/2
    }

    // ç¼©æ”¾ï¼Œä¼ å…¥ flag>0 æ”¾å¤§ï¼Œ <0 ç¼©å° ï¼Œ0å›å½’100%
    function imageScaleAddSub(flag, step=0.1) {
        if(showImage.source == "") return
        // è®¡ç®—ç¼©æ”¾æ¯”ä¾‹
        let s = 1.0 // flag==0 æ—¶å¤åŸ
        if (flag > 0) {  // æ”¾å¤§
            s = (imageScale + step).toFixed(1)
            const imageFullScale = Math.max(flickable.width/imageW, flickable.height/imageH)
            const max = Math.max(imageFullScale, 2.0) // ç¦æ­¢è¶…è¿‡200%æˆ–å›¾ç‰‡å¡«æ»¡å¤§å°
            if(s > max) s = max
        }
        else if(flag < 0) {  // ç¼©å°
            s = (imageScale - step).toFixed(1)
            if(s < 0.1) s = 0.1
        }
        imageScale = s
        updateImageXY()
    }

    // å›¾ç‰‡å¡«æ»¡ç»„ä»¶
    function imageScaleFull() {
        if(showImage.source == "") return
        imageScale = Math.min(flickable.width/imageW, flickable.height/imageH)
        updateImageXY()
    }

    
    // ======================== ã€å¸ƒå±€ã€‘ =========================

    property real imageScale: 1.0 // å›¾ç‰‡ç¼©æ”¾æ¯”ä¾‹
    property int imageW: 0 // å›¾ç‰‡å®½é«˜
    property int imageH: 0
    property bool hasTextBoxes: false // å½“å‰æœ‰æ— æ–‡æœ¬å—
    property bool showTextBoxes: true // æ˜¾ç¤ºæ–‡æœ¬æ¡†
    property var textBoxes: [] // æ–‡æœ¬æ¡†åˆ—è¡¨

    // å›¾ç‰‡åŒºåŸŸ
    Rectangle {
        id: flickableContainer
        anchors.top: parent.top
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: bottomCtrl.top
        anchors.margins: size_.spacing
        anchors.bottomMargin: 0
        color: theme.bgColor

        // æ»‘åŠ¨åŒºåŸŸï¼Œè‡ªåŠ¨ç›‘å¬å·¦é”®æ‹–æ‹½
        Flickable {
            id: flickable
            anchors.fill: parent
            contentWidth: showImageContainer.width
            contentHeight: showImageContainer.height
            clip: true
            
            // å›¾ç‰‡å®¹å™¨ï¼Œå¤§å°ä¸å°äºæ»‘åŠ¨åŒºåŸŸ
            Item {
                id: showImageContainer
                width: Math.max( imageW * imageScale , flickable.width )
                height: Math.max( imageH * imageScale , flickable.height )
                Image {
                    id: showImage
                    anchors.centerIn: parent
                    scale: imageScale

                    // OCR ç»“æœæ–‡æœ¬æ¡†å®¹å™¨
                    Item {
                        visible: hasTextBoxes && showTextBoxes

                        Repeater {
                            model: textBoxes
                            Rectangle {
                                property var info: textBoxes[index]
                                x: info.x
                                y: info.y
                                width: info.width
                                height: info.height
                                // border.width: 1
                                border.width: imageScale>1?1:1/imageScale
                                border.color: "red"
                                color: "#00000000"
                            }
                        }
                    }
                }
            }

            // æ»šåŠ¨æ¡
            ScrollBar.vertical: ScrollBar { }
            ScrollBar.horizontal: ScrollBar { }
        }

        // è¾¹æ¡†
        Rectangle {
            anchors.fill: parent
            color: "#00000000"
            border.width: 1
            border.color: theme.coverColor3
        }

        // ç›‘å¬æ›´å¤šé¼ æ ‡äº‹ä»¶
        MouseArea {
            anchors.fill: parent
            acceptedButtons: Qt.RightButton
            // æ»šè½®ç¼©æ”¾
            onWheel: {
                if (wheel.angleDelta.y > 0) {
                    imageScaleAddSub(1)  // æ”¾å¤§
                }
                else {
                    imageScaleAddSub(-1)  // ç¼©å°
                }
            }
        }
    }

    // åº•éƒ¨æ§åˆ¶æ 
    Item {
        id: bottomCtrl
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        anchors.margins: size_.spacing
        height: size_.text*1.5
        clip: true

        // å·¦
        Row {
            anchors.left: parent.left
            anchors.top: parent.top
            anchors.bottom: parent.bottom

            Button_ {
                anchors.top: parent.top
                anchors.bottom: parent.bottom
                textSize: size_.smallText
                text_: showTextBoxes ? qsTr("æ˜¾ç¤ºæ–‡æœ¬")+" ğŸ”¼" : qsTr("éšè—æ–‡æœ¬")+" ğŸ”½"
                onClicked: showTextBoxes = !showTextBoxes
                visible: hasTextBoxes
            }
        }
        // å³
        Row {
            anchors.right: parent.right
            anchors.top: parent.top
            anchors.bottom: parent.bottom

            // ä¿å­˜
            // IconButton {
            //     anchors.top: parent.top
            //     anchors.bottom: parent.bottom
            //     width: height
            //     icon_: "save"
            //     color: theme.textColor
            //     onClicked: imageScaleFull()
            //     toolTip: qsTr("ä¿å­˜å›¾ç‰‡")
            // }
            // é€‚åˆå®½é«˜
            IconButton {
                anchors.top: parent.top
                anchors.bottom: parent.bottom
                width: height
                icon_: "full_screen"
                color: theme.textColor
                onClicked: imageScaleFull()
                toolTip: qsTr("é€‚åº”çª—å£")
            }
            // 1:1
            IconButton {
                anchors.top: parent.top
                anchors.bottom: parent.bottom
                width: height
                icon_: "one_to_one"
                color: theme.textColor
                onClicked: imageScaleAddSub(0)
                toolTip: qsTr("å®é™…å¤§å°")
            }
            // ç™¾åˆ†æ¯”æ˜¾ç¤º
            Text_ {
                anchors.top: parent.top
                anchors.bottom: parent.bottom
                verticalAlignment: Text.AlignVCenter
                horizontalAlignment: Text.AlignRight
                text: (imageScale*100).toFixed(0) + "%"
                color: theme.subTextColor
                width: size_.text * 2.7
            }
        }
    }
}